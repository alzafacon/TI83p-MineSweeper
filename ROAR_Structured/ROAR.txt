Line(64,8,64,2,0
Text(54,65,"OPENING
1→L
Fill(0,[E]
1→[E](I,J
Repeat not(Ans
    If pxl-Test(I8-1,J6-1
    Then
    //If pixel in center of tile is on (i.e. If not on a revealed 0)
        While J>1 and not([B](I,J
                J-1→J
        End
        //While J>1 and not([B](I,J
            
        If [B](I,J
        Then
        //If on non-0
            If 2≠[E](I,J
            Then
                2→[E](I,J
                Text(I8-4,J6-2,[B](I,J
                J+1→J
            End
            //If [B](I,Ans
            //Go,to OntoNon0
        Else
        //on 0
            
            //If J=1                         This goto should be fine
            //Go,to ZeroFirstEncountered
        End
        //If [B](I,J
    End
    //If pxl-Test(I8-1,J6-1    
    1→F
    //F is TRUE always except when the label F should be used
    //While ((non-0 and NOT(processed)) or NOT(revealed)) and F
    While (([B](I,J) and not([E](I,J))) or (not([B](I,J)) and pxl-Test(I8-1,J6-1))) and F
    //Lbl ZeroFirstEncountered
        If not([B](I,J
        Then
            J>1→G
            If I>1
            Then
                If not([E](I-1,J-Ans
                Then
                    L+1→L
                    I-1→L₅(Ans
                    J-G→L₆(L
                    1→[E](I-1,Ans
                End
                //If not([E](I-1,J-Ans
            End
            //If I>1
            If I<7
            Then
                If not([E](I+1,J-G
                Then
                    L+1→L
                    I+1→L₅(Ans
                    J-G→L₆(L
                    1→[E](I+1,Ans
                End
                //If not([E](I+1,J-G
            End
            //If I<7
            //Label 0
            Text(I8-4,J6-2,"   "
            While J<10 and not([B](I,J
                J+1→J
                If not([B](I,Ans
                Text(I8-4,J6-2,"   "
            End
        End
        //If J=10//cannot be a non-0, must be a zero
        //Go,to Next //this is covered by the second condition of if stmt below(?)
        //Lbl OntoNon0
        If 2≠[E](I,J) and [B](I,J)
        Then
            not([B](I+(I≠7),J))-not([B](I-(I≠1),J→G
            If not(Ans
            Then
            //no zeroes flanking
            //Label Final
                Text(I8-4,J6-2,[B](I,J
                0→F
            Else
            //there is a flanking 0                 
            //Lbl 2    
                Text(I8-4,J6-2,[B](I,J
                2→[E](I,J

                While J<10 and [B](I,J) and not([B](I+G,J)
                    J+1→J
                    If 2≠[E](I,J) and [B](I,J)
                    Then
                        Text(I8-4,J6-2,[B](I,J)
                        If not([B](I+G,J)
                            2→[E](I,J)
                    End
                End

            End
            //If not(Ans
        End
        //If 2≠[E](I,J
    End
    //While (([B](I,J) and not([E](I,J)) or (not([B](I,J)) and pxl-Test(I8-1,J6-1))) and F
    //Lbl Next
    L₅(L→I
    L₆(L→J
    L-1→L
End
getKey
Line(64,8,64,2
Text(54,65,"   PLAY        "